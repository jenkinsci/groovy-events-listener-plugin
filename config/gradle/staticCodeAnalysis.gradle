
//apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'jdepend'


def setDefault(key, value) {
    if (!project.hasProperty(key)) {
        ext[key] = value
    }
}

setDefault("ignoreFailures", false)

if (ignoreFailures) {
    println """
WARNING: ${project.name} - Ignoring Static Code Analysis Failures
"""
}

// turn off html reports... windows can't handle certain cucumber test name characters.
test.reports.getHtml().enabled = false

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

checkstyle {
    ignoreFailures = project.properties["ignoreFailures"]
    sourceSets = [sourceSets.main]
}

jdepend {
    ignoreFailures = project.properties["ignoreFailures"]
    toolVersion = '2.9.1'
}

findbugs {
    ignoreFailures = true // project.properties["ignoreFailures"]
    effort = "max"
    toolVersion = '2.0.3'
}

pmd {
    ignoreFailures = project.properties["ignoreFailures"]
}

configurations.runtime {
    resolutionStrategy {
        failOnVersionConflict()
    }
}
configurations.jdepend.resolutionStrategy {
    force 'jdepend:jdepend:2.9.1'
}
