plugins {
    id 'org.jenkins-ci.jpi' version '0.39.0'
    id 'jacoco'
}

sourceCompatibility = JavaVersion.VERSION_1_8

if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
    throw new GradleException("Build requires Java ${JavaVersion.VERSION_1_8}")
}

jenkinsPlugin {
    coreVersion = '2.143'
    shortName = project.name
    displayName = jenkinsDisplayName
    url = "https://github.com/jenkinsci/$project.name"
    gitHubUrl = "https://github.com/jenkinsci/$project.name"
    compatibleSinceVersion = '1.20'
    fileExtension = 'hpi'
    maskClasses = 'groovy.grape'
    pluginFirstClassLoader = true
    developers {
        developer {
            id 'nickg'
            name 'Nick Grealy'
            email 'nickgrealy@gmail.com'
        }
    }
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.4.21' // add the Groovy lib to the plugin to make @Grab work
    implementation 'org.apache.ivy:ivy:2.4.0'              // required for @Grab

    testImplementation 'io.cucumber:cucumber-junit:4.8.1'
    testImplementation 'io.cucumber:cucumber-java:4.8.1'
    testImplementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
}

defaultTasks 'clean', 'jpi'

clean {
    delete 'work'
    delete '.gradle'
}

tasks.register('deleteTarget', Delete) {
    delete 'target', 'out', 'work'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

// Integration Test Structure
test {
    exclude '**/integration/**'
    reports {
        junitXml.enabled = true
        html.enabled = true
    }
}

tasks.register('integTest', Test) {
    dependsOn = [jpi, test]
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    include '**/integration/**'
    outputs.upToDateWhen { false }
    reports {
        junitXml.enabled = true
        html.enabled = true
    }
}

check.dependsOn 'integTest'

tasks.withType(Test) {
    testLogging {
        exceptionFormat = 'full'
    }
}

clean.dependsOn deleteTarget

wrapper {
    gradleVersion = '6.0.1'
}
