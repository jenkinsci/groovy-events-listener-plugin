
plugins {
    id 'org.jenkins-ci.jpi' version '0.43.0'
    id 'jacoco'
}

defaultTasks 'clean', 'jpi'

clean {
    delete 'work'
    delete '.gradle'
}

/**
=== Usage ===

gradlew jpi - Build the Jenkins plugin file, which can then be found in the build directory. The file will currently end in ".hpi".
gradlew publishToMavenLocal - Build the Jenkins plugin and install it into your local Maven repository.
gradlew publish - Deploy your plugin to the Jenkins Maven repository to be included in the Update Center.
gradlew server - Start a local instance of Jenkins (http://localhost:8080) with the plugin pre-installed for testing and debugging.
 */

apply plugin: 'org.jenkins-ci.jpi'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
    jcenter()
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

jenkinsPlugin {
    coreVersion = '2.143'
    shortName = project.name
    displayName = jenkinsDisplayName
    url = "https://github.com/jenkinsci/$project.name"
    gitHubUrl = "https://github.com/jenkinsci/$project.name"
    compatibleSinceVersion = '1.20'
    fileExtension = 'hpi'
    developers {
        developer {
            id 'nickg'
            name 'Nick Grealy'
            email 'nickgrealy@gmail.com'
        }
    }
}

configurations { forceGroovy }

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.4.12' // add the Groovy lib to the plugin to make @Grab work
    implementation 'org.apache.ivy:ivy:2.4.0'              // required for @Grab

    testImplementation 'io.cucumber:cucumber-junit:4.8.1'
    testImplementation 'io.cucumber:cucumber-java:4.8.1'
    testImplementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
}

jpi {
    classpath configurations.forceGroovy // add the Groovy lib to the plugin to make @Grap work
}

/**
 * We must exclude the junit-dep dependency, because it is clashing with junit when we run the AcceptanceTests.
 */
configurations.findAll {!it.name.endsWith('junit-dep')}.each { conf ->
    conf.exclude group: "junit", module: "junit-dep"
}

wrapper {
    gradleVersion = '7.3.3'
}
