buildscript {
    repositories {
        jcenter()
        maven {
            url 'http://repo.jenkins-ci.org/releases/'
        }
    }
    dependencies {
        classpath 'org.jenkins-ci.tools:gradle-jpi-plugin:0.18.1'
    }
}

defaultTasks 'clean', 'jpi'

/**
=== Usage ===

gradlew jpi - Build the Jenkins plugin file, which can then be found in the build directory. The file will currently end in ".hpi".
gradlew publishToMavenLocal - Build the Jenkins plugin and install it into your local Maven repository.
gradlew publish - Deploy your plugin to the Jenkins Maven repository to be included in the Update Center.
gradlew server - Start a local instance of Jenkins (http://localhost:8080) with the plugin pre-installed for testing and debugging.
 */

ext.enforceJavaVersion = JavaVersion.VERSION_1_8

apply plugin: 'org.jenkins-ci.jpi'
apply from: "$rootDir/gradle/common.gradle"
apply plugin: 'jacoco'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'http://repo.jenkins-ci.org/releases/'
    }
}

ext.'jenkins.httpPort' = 8080

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

// Integration Test Structure
test {
    exclude '**/integration/**'
    reports {
        junitXml.enabled = true
        html.enabled = true
    }
}

task integTest(type: Test, dependsOn: [jpi, test]) {
    testClassesDir = sourceSets.test.output.classesDir
    classpath = sourceSets.test.runtimeClasspath
    include '**/integration/**'
    outputs.upToDateWhen { false }
    reports {
        junitXml.enabled = true
        html.enabled = true
    }
}

check.dependsOn integTest

jenkinsPlugin {
    coreVersion = '2.60.3'
    shortName = project.name
    displayName = jenkinsDisplayName
    url = "https://wiki.jenkins-ci.org/display/JENKINS/$jenkinsDisplayName"
    gitHubUrl = "https://github.com/jenkinsci/$project.name"
    compatibleSinceVersion = '1.20'
    fileExtension = 'hpi'
    maskClasses = 'groovy.grape'
    developers {
        developer {
            id 'nickg'
            name 'Nick Grealy'
            email 'nickgrealy@gmail.com'
        }
    }
}

configurations { forceGroovy }

dependencies {
    forceGroovy 'org.codehaus.groovy:groovy-all:2.4.8'
    compile 'org.apache.ivy:ivy:2.4.0' // For @Grab

    testCompile('org.hamcrest:hamcrest-core:1.3') { force = true }
    testCompile('junit:junit:4.12') { force = true }
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'info.cukes:cucumber-junit:1.2.4'
    testCompile 'info.cukes:cucumber-java:1.2.4'
    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'

    jenkinsTest 'org.jenkins-ci.main:jenkins-test-harness:2.48'
}

task deleteTarget(type: Delete) {
    delete 'target', 'out', 'work'
}

clean.dependsOn deleteTarget

jpi {
    classpath configurations.forceGroovy // add the Groovy lib to the plugin to make @Grab work
    manifest.attributes(['PluginFirstClassLoader': 'true'])
}

/**
 * We must exclude the junit-dep dependency, because it is clashing with junit when we run the AcceptanceTests.
 */
configurations.findAll { !it.name.endsWith('junit-dep') }.each { conf ->
    conf.exclude group: 'junit', module: 'junit-dep'
}
